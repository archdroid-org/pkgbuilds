# Maintainer: Jefferson Gonzalez <jgmdev@gmail.com>
# Contributor: Brett Cornwall <ainola@archlinux.org>
# Contributor: Maxim Baz <$pkgname at maximbaz dot com>
# Contributor: Omar Pakker

pkgname=wayfire-wlroots
_pkgname=wlroots
pkgver=0.12.0
pkgrel=1
license=('MIT')
pkgdesc='Modular Wayland compositor library'
url='https://github.com/swaywm/wlroots'
arch=('x86_64' 'aarch64')
install=wayfire-wlroots.install
depends=(
    'libinput'
    'libxkbcommon'
    'opengl-driver'
    'pixman'
    'xcb-util-errors'
    'xcb-util-renderutil'
    'xcb-util-wm'
)
makedepends=(
    'meson'
    'ninja'
    'wayland-protocols'
    'xorg-xwayland'
)
optdepends=(
    'xorg-xwayland: Xwayland support'
)
provides=(
    'libwlroots.so'
)
source=(
    "$_pkgname-$pkgver.tar.gz::https://github.com/swaywm/wlroots/archive/$pkgver.tar.gz"
    "wayfire-wlroots.conf"
)
md5sums=(
    'bc9dbfef37385dbe0f4fe129d2329be5'
    '9192c7952487002c82628432c900d765'
)

build() {
    arch-meson "$_pkgname-$pkgver" build \
        --prefix /opt/wayfire-wlroots \
        -Dlogind-provider=systemd \
        -Dlibseat=disabled
    ninja -C build
}

package() {
    DESTDIR="$pkgdir" ninja -C build install
    install -Dm644 "$_pkgname-$pkgver/LICENSE" -t "$pkgdir/opt/wayfire-wlroots/share/licenses/$_pkgname/"
    install -Dm644 "wayfire-wlroots.conf" -t "$pkgdir/etc/ld.so.conf.d/"
}
