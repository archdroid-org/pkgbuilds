# U-Boot: ODROID-C4/HC4
#
# Maintainer: Jefferson Gonzalez <jgmdev@gmail.com>

buildarch=8

pkgname=uboot-odroid-c4
_revision=189
pkgver=2015.01.${_revision}
pkgrel=1
_edid_generator_commit=31a6f80784d289d2faa8c4ca4788409c83b3ea14
pkgdesc="U-Boot for ODROID-C4/HC4"
arch=('aarch64')
url="https://github.com/hardkernel/u-boot"
license=('GPL')
install=$pkgname.install
depends=('uboot-tools' 'util-linux' 'gawk' 'sed')
conflicts=('uboot-odroid-n2')
source=(
  "https://github.com/hardkernel/u-boot/releases/download/travis/odroidc4-${_revision}/u-boot-odroidc4-${_revision}.tar.gz"
  "https://github.com/akatrevorjay/edid-generator/archive/${_edid_generator_commit}.tar.gz"
  "edid.md"
  'sd_fusing.sh'
  '91-uboot-uimg.hook'
  '92-uboot-bootini.hook'
  'boot.mainline.ini'
  'boot.hardkernel.ini'
  'odroid-bootini'
)
md5sums=(
  'f4e88cfd709ca73203de00c0b28f8f82'
  '4be5fae86f996be8540a4344a8b8c120'
  '01aedffd498933b92c2573f779d6562b'
  '3bd43e59c5f88a7544505288627fe2dd'
  '1931c8dfde7088530f173ca59fdb8989'
  'e1a78b20ea7fe68ed3bb6fe24ff54110'
  'a0925c3da4b399528a07502852d48b5d'
  '0c436689f5a6c85a2f569322d182a928'
  'a454262534c21d5b263bca6ec0141ae5'
)

package() {
  cd sd_fuse

  mkdir -p "${pkgdir}"/boot
  cp u-boot.bin "${pkgdir}"/boot
  cp "${srcdir}/sd_fusing.sh" "${pkgdir}"/boot

  install -Dm644 "${srcdir}/91-uboot-uimg.hook" "${pkgdir}/usr/share/libalpm/hooks/91-uboot-uimg.hook"
  install -Dm644 "${srcdir}/92-uboot-bootini.hook" "${pkgdir}/usr/share/libalpm/hooks/92-uboot-bootini.hook"

  install -Dm644 "${srcdir}/boot.hardkernel.ini" "${pkgdir}/usr/share/${pkgname}/boot.hardkernel.ini"
  install -Dm644 "${srcdir}/boot.mainline.ini" "${pkgdir}/usr/share/${pkgname}/boot.mainline.ini"

  install -Dm755 "${srcdir}/odroid-bootini" "${pkgdir}/usr/bin/odroid-bootini"

  mkdir -p "${pkgdir}"/usr/lib/firmware/edid
  cp "${srcdir}/edid-generator-${_edid_generator_commit}/"*.bin "${pkgdir}"/usr/lib/firmware/edid/
  cp "${srcdir}/edid.md" "${pkgdir}"/usr/lib/firmware/edid/README.md
}
