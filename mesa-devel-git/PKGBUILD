# Maintainer: Jefferson Gonzalez <jgmdev@gmail.com>

pkgbase=mesa-devel-git
pkgname=('mesa-devel-git')
pkgdesc="Open-source implementation of the OpenGL specification (development version)"
pkgver=22.0.0_devel.148059.b05d228695b
pkgrel=2
arch=('aarch64')
makedepends=(
  'python-mako' 'libxml2' 'libx11' 'libdrm'
  'libxshmfence' 'libxxf86vm' 'libxdamage' 'libvdpau' 'libva'
  'wayland' 'wayland-protocols' 'elfutils' 'libomxil-bellagio'
  'clang' 'libglvnd' 'lm_sensors' 'libxrandr' 'meson'
)
depends=(
  'libdrm' 'libxxf86vm' 'libxdamage' 'libxshmfence' 'libelf'
  'libomxil-bellagio' 'libunwind' 'libglvnd' 'llvm-libs' 'lm_sensors'
  'libclc' 'glslang' 'vulkan-icd-loader' 'wayland' 'zstd'
)
optdepends=('clang: opencl' 'compiler-rt: opencl')
provides=(
  'vulkan-mesa-layers'
  'opencl-mesa'
  'opencl-driver'
  'vulkan-swrast'
  'libva-mesa-driver'
  'mesa-vdpau'
  'mesa'
  'mesa-dri'
  'mesa-libgl'
  'opengl-driver'
)
conflicts=(
  'vulkan-mesa-layers'
  'opencl-mesa'
  'vulkan-swrast'
  'libva-mesa-driver'
  'mesa-vdpau'
  'mesa'
  'mesa-dri'
  'mesa-libgl'
)
replaces=('mesa-dri' 'mesa-libgl' 'mesa-arm-git')
url="https://www.mesa3d.org/"
license=('custom')
source=(
  'mesa::git+https://github.com/mesa3d/mesa.git'
  'LICENSE'
)
md5sums=(
  'SKIP'
  '1eff81373cde4b3d7b0ea7590431c962'
)

pkgver() {
  cd mesa
  read -r _ver <VERSION
  echo ${_ver/-/_}.$(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  meson setup mesa build \
    --buildtype=release \
    -D zstd=enabled \
    -D b_lto=false \
    -D prefix=/usr \
    -D sysconfdir=/etc \
    -D b_ndebug=true \
    -D platforms=x11,wayland \
    -D gallium-drivers=nouveau,panfrost,kmsro,swrast,virgl,zink,etnaviv,lima,v3d,vc4,tegra,freedreno \
    -D vulkan-drivers=swrast,broadcom,freedreno,panfrost \
    -D vulkan-layers=device-select,overlay \
    -D dri3=enabled \
    -D egl=enabled \
    -D gallium-extra-hud=true \
    -D gallium-nine=false \
    -D gallium-omx=bellagio \
    -D gallium-opencl=icd \
    -D gallium-va=enabled \
    -D gallium-vdpau=enabled \
    -D gallium-xa=disabled \
    -D gallium-xvmc=disabled \
    -D gbm=enabled \
    -D gles1=disabled \
    -D gles2=enabled \
    -D glvnd=true \
    -D glx=dri \
    -D libunwind=disabled \
    -D llvm=enabled \
    -D lmsensors=enabled \
    -D osmesa=true \
    -D shared-glapi=enabled \
    -D microsoft-clc=disabled \
    -D valgrind=disabled

  # Print config
  meson configure build

  ninja $NINJAFLAGS -C build
}

package() {
  DESTDIR="$pkgdir" ninja $NINJAFLAGS -C build install

  # indirect rendering
  ln -s /usr/lib/libGLX_mesa.so.0 ${pkgdir}/usr/lib/libGLX_indirect.so.0

  install -Dt "$pkgdir"/usr/share/licenses/$pkgname "$srcdir"/LICENSE
}
